dnl Process this file with autoconf to produce a configure script.

dnl For some reason the default prefix was set to NONE on Solaris.
AC_PREFIX_DEFAULT(/usr/local)

AC_INIT(coda-src/librepair/resolve.h)
AC_CONFIG_HEADER(config.h)

AC_CONFIG_AUX_DIR(configs)
dnl Set up the environment for djgpp/cygwin cross compilation.
sinclude(configs/coda_cross.m4)
dnl Load the CODA_ tests.
sinclude(configs/coda_macros.m4)

CODA_OPTION_SUBSYS(lwp)
CODA_OPTION_SUBSYS(rvm)
CODA_OPTION_SUBSYS(rpc2)
CODA_OPTION_OPENSSL

dnl Checks for programs.
AC_PROG_CC
AC_PROG_CXX
CODA_PROG_NATIVECC

dnl Now we can derive things like systype/cputype/initdir
sinclude(configs/codaconf.m4)

AC_PROG_CPP
AC_PROG_CXXCPP
AC_PROG_RANLIB
AC_CHECK_PROG(AR, ar, ar)
AC_CHECK_PROG(AS, as, as)
AC_PROG_LEX
AC_PROG_YACC
AC_PROG_INSTALL
AC_PROG_AWK
AC_PATH_PROG(CP, cp, "")
AC_PATH_PROG(DF, df, "")
AC_PATH_PROG(ED, ed, "")

dnl check to see if ED is defined ...
if test x${ED} = x ; then
  echo 'program "ed" not found.'
  echo 'coda needs the program "ed" for proper operation.'
  exit 1
fi

AC_PATH_PROG(GREP, grep, "")
AC_PATH_PROG(HOSTNAME, hostname, "")
AC_PATH_PROG(MV, mv, "")
AC_PATH_PROG(PERL, perl, "")
AC_PATH_PROG(RM, rm, "")
AC_PATH_PROG(RDSINIT, rdsinit, "", $PATH:/usr/sbin:/usr/local/sbin:/usr/pkg/sbin)
AC_PATH_PROG(RVMUTL, rvmutl, "", $PATH:/usr/sbin:/usr/local/sbin:/usr/pkg/sbin)
dnl AC_PATH_PROGS(TIXWISH, [tixwish tixwish-tk4.1 tixwish4.1.8.0], "/usr/bin/tixwish")

MV="${MV} -f"
RM="${RM} -f"

dnl Checks for libraries.
CODA_FIND_LIB(lwp, [#include <lwp/lwp.h>], [LWP_Init(0,0,0)])
CODA_FIND_LIB(rvm, [#include <rvm/rvm.h>], [rvm_initialize(0,0)])
CODA_FIND_LIB(rpc2, [#include <rpc2/rpc2.h>], [RPC2_Init(0,0,0,0,0)], -llwp)

CODA_CHECK_LIBTERMCAP
CODA_CHECK_LIBCURSES
CODA_CHECK_READLINE
CODA_CHECK_LIBDB185
CODA_CHECK_MD5

AC_SEARCH_LIBS(bcopy, ucb)
AC_SEARCH_LIBS(bind, socket)
AC_SEARCH_LIBS(inet_ntoa, nsl)


dnl Checks for header files.
AC_CHECK_HEADERS(search.h sys/resource.h sys/stream.h ncurses.h)
AC_CHECK_HEADERS(sys/statvfs.h sys/statfs.h sys/mount.h sys/vfs.h)
AC_CHECK_HEADERS(sys/types.h sys/time.h sys/select.h sys/socket.h sys/ioccom.h)
AC_CHECK_HEADERS(arpa/inet.h arpa/nameser.h netinet/in.h sys/un.h osreldate.h)

dnl Checks for typedefs.
AC_CHECK_TYPE(int32_t, int)
AC_CHECK_TYPE(u_int32_t, unsigned int)
CODA_CHECK_TYPE(socklen_t, int, sys/socket.h)

dnl Checks for structures.

dnl Checks for compiler characteristics.
CODA_CC_FEATURE_TEST(no_exceptions, -fno-exceptions)
CODA_CXX_FEATURE_TEST(no_exceptions, -fno-exceptions)
CODA_CXX_FEATURE_TEST(no_operator_names, -fno-operator-names)
CODA_DARWIN_BROKEN_CPP_WORKAROUND

dnl Checks for library functions.
AC_CHECK_FUNCS(inet_aton inet_ntoa)
AC_CHECK_FUNCS(scandir select setenv snprintf statfs strerror strtol)
AC_FUNC_MMAP
AC_FUNC_SELECT_ARGTYPES
CODA_CHECK_FILE_LOCKING
CODA_CHECK_BCOPY
CODA_CHECK_DIRENT
CODA_RFLAG

dnl Checks for system services.

dnl  Substitute the top of the object directory
TOPOBJ=`pwd`
AC_SUBST(TOPOBJ)
AC_SUBST(shortsys)

AC_PROG_MAKE_SET
AC_OUTPUT(Makefile Makeconf.setup coda_config.h
configs/Makefile 
lib-src/Makefile 
lib-src/base/Makefile
lib-src/kernel-includes/Makefile 
lib-src/special-includes/Makefile 
coda-src/Makefile 
coda-src/scripts/Makefile 
coda-src/scripts/bldvldb.sh
coda-src/scripts/codaconfedit
coda-src/scripts/createvol_rep
coda-src/scripts/rc.vice
coda-src/scripts/startserver
coda-src/scripts/venus.init
coda-src/scripts/vice-setup
coda-src/scripts/vice-setup-rvm
coda-src/scripts/vice-setup-scm
coda-src/scripts/vice-setup-srvdir
coda-src/scripts/vice-setup-user
coda-src/scripts/vutil
coda-src/kerndep/Makefile 
coda-src/partition/Makefile 
coda-src/dir/Makefile  
coda-src/portmapper/Makefile 
coda-src/util/Makefile 
coda-src/al/Makefile 
coda-src/vicedep/Makefile  
coda-src/auth2/Makefile 
coda-src/vv/Makefile 
coda-src/vol/Makefile 
coda-src/librepair/Makefile 
coda-src/venus/Makefile 
coda-src/sidekick/Makefile 
coda-src/repair/Makefile 
coda-src/resolution/Makefile 
coda-src/volutil/Makefile 
coda-src/vtools/Makefile 
coda-src/vice/Makefile 
coda-src/update/Makefile 
coda-src/norton/Makefile 
coda-src/asr/Makefile 
coda-src/egasr/Makefile  
utils-src/Makefile 
utils-src/potemkin/Makefile 
utils-src/potemkin/ds/Makefile 
utils-src/potemkin/potemkin/Makefile  
cygwin/Makefile 
cygwin/sys/Makefile
djgpp/Makefile 
djgpp/relay/Makefile 
djgpp/inject/Makefile 
djgpp/tests/Makefile
coda-src/partition/tests/Makefile 
coda-src/util/tests/Makefile 
coda-src/venus/tests/Makefile  
coda-src/smon2/Makefile)
